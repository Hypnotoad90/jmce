// Client-side related functions.
//
// Author: 
// Positron
//
// License:
// Free for any purpose. I'm not liable for any damages.

#library "client"
#include "zcommon.acs"

// Player number retrival.
// Thanks to Euranna for the solution and helping me understand it.
// Source: http://www.skulltag.com/forum/viewtopic.php?p=310483#p310483

// -2 indicates the library has not been initialized.
int _cl_num = -2;

script 940 OPEN CLIENTSIDE {
   // Force player to execute a script upon connecting to the server.
   // Offline, this script takes more than one tic to execute, so any ENTER
   // scripts should be aware of this behavior.
   consoleCommand( "puke 941" );
}

script 941 ( void ) NET CLIENTSIDE {
   // Player should be the activator of this script. We can now save their
   // player number for later comparisons.
   _cl_num = playerNumber();
}

function int ClientGetNumber( void ) {
   return _cl_num;
}

// ----------------------------------------------------------------

// Executes client-side script for player with given player number.
function void ClientExecute( int player, int scriptNum ) {
   ClientExecuteArg( player, scriptNum, 0 );
}

// Same as ClientExecute(), but allows including script arguments. Currently,
// only one script argument is supported.
function void ClientExecuteArg( int player, int scriptNum, int arg1 ) {
   acs_Execute( 943, 0, player, scriptNum, arg1 );
}

// NOTE: It's possible to combine the player and scriptNum variables into
// a single variable so we can allow for an extra script argument.
script 943 ( int player, int scriptNum, int arg1 ) CLIENTSIDE {
   if ( player == _cl_num ) {
      acs_Execute( scriptNum, 0, arg1 );
   }
}