//Monster-Death Incarnate
ACTOR Incarnate 30115 
{ 
Health 300 
Radius 20 
Height 56 
Mass 500 
Speed 12
MeleeDamage 7
PainChance 64 
MONSTER
Damagefactor hitscan, 0
+FLOORCLIP 
+SHORTMISSILERANGE
+DONTHURTSPECIES
Obituary "%o was killed by a Death Incarnate's glare."
HitObituary "%o was beaten to death by a Death Incarnate."
SeeSound "monster/incsit" 
PainSound "skeleton/pain" 
DeathSound "monster/incdth" 
ActiveSound "monster/incact"
AttackSound "monster/incatk"
MeleeSound "monster/inchit"
States 
   { 
   Spawn: 
       INCA AB 10 A_Look 
       Loop 
   See: 
       INCA AABBCCDDEEFF 2 A_Chase 
       Loop 
   Melee:
       INCA G 0 A_Jump(128,5)
       INCA G 0 A_FaceTarget 
       INCA G 6 A_SkelWhoosh 
       INCA H 6 A_FaceTarget 
       INCA I 6 A_MeleeAttack
       Goto See
       INCA R 0 A_FaceTarget 
       INCA R 5 A_SkelWhoosh 
       INCA S 5 A_FaceTarget 
       INCA T 5 A_MeleeAttack
       INCA G 0 A_FaceTarget 
       INCA G 5 A_SkelWhoosh 
       INCA H 5 A_FaceTarget 
       INCA I 5 A_MeleeAttack
       Goto See 
   Missile: 
       INCA J 5 A_FaceTarget
       INCA J 5
       INCA U 10 Bright A_CustomBulletAttack(30,0,5,3,"DIpuff",1024)
       INCA K 10
       Goto See 
   Pain: 
       INCA L 5 
       INCA L 5 A_Pain 
       Goto See 
   Death:
       INCA L 0 A_Jump(192,7)
       INCA LM 7 
       INCA N 7 A_Scream 
       INCA O 7 A_NoBlocking 
       INCA P 7 
       INCA Q -1 
       Stop
       INCA LM 7 
       INCA N 7 A_Scream 
       INCA O 7 A_NoBlocking
       INCA P 7
       INCA Q 0 A_Jump(192,4)
       INCA Q 0 A_Jump(128,4)
       INCA Q 0 A_Jump(96,4)
       INCA Q 0 A_Jump(64,4)
       INCA Q 115 
       INCA Q 115 
       INCA Q 115 
       INCA Q 115 
       INCA Q 115
       INCA Q -1 Thing_Raise(0)       
       Stop
   XDeath: 
       INCX A 10 Bright A_Playsound("monster/incexp")
       INCX BC 5 Bright
       INCX D 5 Bright A_NoBlocking
       INCX D 0 Bright A_SpawnItem("HS",0,32) 
       INCX EFGHIJ 5 Bright
       INCX K -1 
   Raise:
       INCA Q 0 A_KillChildren
       INCA QPONML 5
       Goto See 
   } 
}

ACTOR DIPuff
{   
   Radius 0
   Height 1
   Speed 0
   PROJECTILE
   RENDERSTYLE NONE
   DamageType hitscan
   States
   {
   Spawn:
      NULL A 2 Bright
      Stop 
   }
}
//End of Incarnate
//Hades Sphere

ACTOR HS 30116 
{ 
Health 35
Radius 16 
Height 32 
Mass 3000 
Speed 10
ExplosionDamage 128
ExplosionRadius 112
RENDERSTYLE Normal
Obituary "%o was blown away by a Hades Sphere."
PAINCHANCE 0 
MONSTER
+LOOKALLAROUND
+DONTGIB
+NOTARGET
+NOGRAVITY 
+FLOAT
+DONTFALL
+DONTHURTSPECIES
+LONGMELEERANGE
SeeSound "monster/hadsit"
States 
   { 
   Spawn:
       NULL A 1 Bright A_Look      
       Loop
   See:
       HADE EFGH 4 Bright
       HADE A 2 Bright A_Playsound("monster/hadwlk")
       HADE ABBCCDD 2 Bright A_Chase
       HADE D 0 Bright A_Jump (32,1) 
       Goto See+4
       HADE H 0 Bright A_UnSetSolid
       HADE H 0 Bright A_UnSetShootable
       HADE H 5 Bright A_Playsound("monster/hadtel")
       HADE GFEIJKL 4 Bright
       NULL AAAAAAAA 2 Bright A_Chase
       NULL A 0 Bright A_Jump (96,1) 
       Goto See+24
       HADE L 3 Bright A_Playsound("monster/hadtel")
       HADE KJIEFG 4 Bright
       HADE H 0 Bright A_SetShootable
       HADE H 0 Bright A_SetSolid
       Goto See+4
   Melee:
       HADE H 0 A_SetInvulnerable
       HADE HGFE 4 Bright
       HADE E 0 A_UnSetInvulnerable
       HADE E 0 A_Die
       stop
   Death:
       HADE E 0 Bright A_Noblocking
       HADE E 0 Bright A_CustomMissile("HSBoom",0,0,0,0)
       Stop
   XDeath:
       HADE N 0 Bright A_Noblocking
       HADE H 5 Bright A_PlaySound("monster/haddth")
       HADE GFE 4 Bright
       HADE RSTUVWX 4 Bright
       Stop
   } 
}

ACTOR HSBoom
{
   Radius 1
   Height 1
   Speed 0
   Damage 0
   Alpha 0.90
   ExplosionDamage 112
   ExplosionRadius 112
   PROJECTILE
   RENDERSTYLE ADD
   DONTHURTSHOOTER
   SeeSound "monster/hadexp"
   States
   {
   Spawn:
      NULL A 0 Bright
      Goto Death
   Death:
      HADE M 4 Bright A_Explode
      HADE NOPQ 5 Bright
      Stop
   }
}
//End of Hades Sphere