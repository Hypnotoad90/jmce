actor NcjChair 24100 {
    //$Category NotCXJump Decoration
    //$Title "Chair"

    +Solid

    Height 30
    Radius 15

    States {
        Spawn:
            CHAI A -1
            Stop
    }
}

actor NcjMarineSpawner : RandomSpawner 24101 {
    //$Category NotCXJump Decoration
    //$Title "Random Spectating Marine"
    //$Sprite MRNWA0

    +CLIENTSIDEONLY

    Radius 20
    Height 56

    DropItem "WaveMarine", 128
    DropItem "FistMarine", 128
    DropItem "HeadbangMarine", 128
}

actor Jimmothy 24102 {
    //$Category NotCXJump Decoration
    //$Title "Jimmothy"

    +SOLID

    Monster
    Radius 32
    Height 83
    Mass 400
    Health 999999
    PainChance 255
    Obituary "%o was blasted by Jimmothy. What a geezer."
    PainSound "notcxjump/jimmothy/pain"

    States {
        Spawn:
            JIMM A 1 A_LookEx(0, 0, 0, 0, 360, "See")
            Loop

        See:
            JIMM A 1
            Loop

        Pain:
            JIMM A 1 A_SetTranslucent(1.0, 2)
            JIMM A 0 A_FaceTarget
            JIMM AAAAAAAAAA 2 A_CustomRailgun(25, 0, "FF0000", "FF00FF")
            JIMM A 1 A_SetTranslucent(1.0, 0)
            JIMM A 1 A_Pain
            Goto Spawn
    }
}

actor NcjStaticBarrel 24103 {
    //$Category NotCXJump Decoration
    //$Title "Static Barrel"
    //$NotAngled

    +SOLID

    Radius 10
    Height 42

    States {
        Spawn:
            BAR1 AB 6
            Loop
    }
}